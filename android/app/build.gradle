ext {
    kotlin_version = '1.7.10'
}
def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystorePropertiesFile.withReader('UTF-8') { reader ->
        keystoreProperties.load(reader)
    }
}
def requiredSigningKeys = ['keyAlias', 'keyPassword', 'storePassword', 'storeFile']
boolean hasReleaseKeystore = requiredSigningKeys.every { key ->
    def value = keystoreProperties.getProperty(key)
    return value != null && !value.trim().isEmpty()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    namespace "th.mi.navy.navy_encrypt"

    compileSdkVersion 34
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "th.mi.navy.navy_encrypt"
        minSdkVersion 28
        targetSdkVersion 34
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-build-configuration.
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
    }

    signingConfigs {
        if (hasReleaseKeystore) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storePassword keystoreProperties['storePassword']
                storeFile file(keystoreProperties['storeFile'])
            }
        }
    }

    buildTypes {
        release {
            if (hasReleaseKeystore) {
                signingConfig signingConfigs.release
            } else {
                def missingConfigMessage = 'Release signing configuration is missing. Provide android/key.properties with keyAlias, keyPassword, storePassword, and storeFile entries.'
                if (System.getenv('CI')?.toBoolean()) {
                    throw new GradleException(missingConfigMessage)
                }
                println "${missingConfigMessage} Falling back to debug signing for local builds."
                signingConfig signingConfigs.debug
            }
        }
    }
    buildFeatures {
        viewBinding true
    }
    viewBinding {
        enabled true
    }
    buildToolsVersion '34.0.0'
}

flutter {
    source '../..'
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.10'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.7.10'
}
