def kotlin_version = rootProject.ext.kotlin_version
def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw  GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    def env = { key ->
        def value = System.getenv(key)
        return value != null && value.trim().length() > 0 ? value.trim() : null
    }
    keystoreProperties['storeFile'] = env('ANDROID_KEYSTORE_PATH')
    keystoreProperties['storePassword'] = env('ANDROID_KEYSTORE_PASSWORD')
    keystoreProperties['keyAlias'] = env('ANDROID_KEY_ALIAS')
    keystoreProperties['keyPassword'] = env('ANDROID_KEY_PASSWORD')
}

def hasSigningConfig = keystoreProperties['storeFile'] &&
        keystoreProperties['storePassword'] &&
        keystoreProperties['keyAlias'] &&
        keystoreProperties['keyPassword']

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    namespace "th.mi.navy.navy_encrypt"
    compileSdkVersion 34

    defaultConfig {
        applicationId "th.mi.navy.navy_encrypt"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
        versionNameSuffix '5'
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            if (hasSigningConfig) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                def storeFilePath = keystoreProperties['storeFile']
                storeFile storeFilePath ? file(storeFilePath) : null
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            signingConfig hasSigningConfig ? signingConfigs.release : signingConfigs.debug

        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += ['-Xjvm-default=all']
    }

    buildFeatures {
        viewBinding true
    }
    packagingOptions {
        resources {
            excludes += [
                    'META-INF/LICENSE',
                    'META-INF/LICENSE.txt',
                    'META-INF/NOTICE',
                    'META-INF/NOTICE.txt'
            ]
        }
    }
    buildToolsVersion '34.0.0'
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation platform("com.google.firebase:firebase-bom:34.4.0")
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.android.gms:play-services-auth:20.7.0'
    implementation 'androidx.multidex:multidex:2.0.1'
}

apply plugin: 'com.google.gms.google-services'
